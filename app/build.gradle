apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'android-soexcluder'

android {
    //noinspection GradleCompatible
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }
    externalNativeBuild {
        cmake {
            path "src/skia/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk false
        }
    }
    flavorDimensions "default"
    productFlavors {
        canvas {
            buildConfigField("int", "CHANCE", "16")// percent per frame
            buildConfigField("int", "SPEED", "300")// pixels per second
        }
        skia {
            buildConfigField("int", "CHANCE", "16")
            buildConfigField("int", "SPEED", "300")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

soexcluder {
    canvas {
        exclude "**/*"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0"
    implementation "org.jetbrains.anko:anko-commons:0.10.8"
    implementation "org.jetbrains.anko:anko-sdk15:0.10.8"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.exoplayer:exoplayer:2.10.4'
    implementation 'com.jakewharton.timber:timber:4.7.1'
}
